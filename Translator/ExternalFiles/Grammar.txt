1. <signal-program> —> <program>

2. <program> —> PROGRAM <procedure-identifier> ; <block>.

3. <block> —> <variable-declarations> BEGIN <statements-list> END

4. <variable-declarations> —> VAR <declarations-list> | <empty>

5. <declarations-list> —> <declaration> <declarations-list> | <empty>

6. <declaration> —><variable-identifier>: INTEGER ;

7. <statements-list> —> <statement> <statements-list> | <empty>

8 <statement> —> <variable-identifier> := <expression> ;

9 <expression> —> <summand> <summands-list> | - <summand> <summands-list>

10. <summands-list> —> <add-instruction> <summand> <summands-list> | <empty>

11. <add-instruction> —> + | -

12. <summand> —> <multiplier><multipliers-list>

13. <multipliers-list> —> <multiplication-instruction> <multiplier><multipliers-list> | <empty>

14. <multiplication-instruction> —> * | /

15. <multiplier> —> <variable-identifier> | <unsigned-integer> | ( <expression> )

16. <variable-identifier> —> <identifier>

17. <procedure-identifier> —> <identifier>

18. <identifier> —> <letter><string>

19. <string> —> <letter><string> | <digit><string> | <empty>

20. <unsigned-integer> —> <digit><digits-string>

21. <digits-string> —> <digit><digits-string> | <empty>

22. <digit> —> 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9

23. <letter> —>A|B|C|D|...|Z